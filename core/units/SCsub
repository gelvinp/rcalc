Import("env")
from glob import glob

import units_builders

env.units_sources = []

env_units = env.Clone()

env_units.Depends("#core/units/units.gen.cpp", glob("*.inc"))
env_units.Depends("#core/units/units.gen.cpp", "#core/units/units_builders.py")
env_units.CommandNoCache(
    "#core/units/units.gen.cpp",
    glob("*.inc"),
    env.Run(units_builders.make_units_maps, "Building units map.")
)

env_units.add_source_files(env.units_sources, "*.cpp")

env_units_gen = env_units.Clone()
if "-Werror" in env_units_gen["CCFLAGS"]:
    env_units_gen["CCFLAGS"].remove("-Werror")
env_units_gen.add_source_files(env.units_sources, "#core/units/units.gen.cpp")

lib = env_units.add_library("units", env.units_sources)
env.Prepend(LIBS=[lib])