Import("env")
from glob import glob

import operators_builders

env.operator_sources = []

env_ops = env.Clone()

env_ops.Depends(glob("*.inc"), "#app/operators/operators_internal.gen.h")
env_ops.Depends("#app/operators/operators.gen.cpp", "#app/operators/operators_internal.gen.h")
env_ops.Depends("#app/operators/operators_internal.gen.h", "#app/operators/operators_builders.py")
env_ops.CommandNoCache(
    "#app/operators/operators_internal.gen.h",
    None,
    env.Run(operators_builders.make_internal_header, "Building operators internal header.")
)

env_ops.Depends("#app/operators/operators.gen.cpp", glob("*.inc"))
env_ops.Depends("#app/operators/operators.gen.cpp", "#app/operators/operators_builders.py")
env_ops.CommandNoCache(
    "#app/operators/operators.gen.cpp",
    glob("*.inc"),
    env.Run(operators_builders.make_operators_map, "Building operators map.")
)

env_ops.add_source_files(env.operator_sources, "*.cpp")

env_ops_gen = env_ops.Clone()
if "-Werror" in env_ops_gen["CCFLAGS"]:
    env_ops_gen["CCFLAGS"].remove("-Werror")
env_ops_gen.add_source_files(env.operator_sources, "#app/operators/operators.gen.cpp")

lib = env_ops.add_library("operators", env.operator_sources)
env.Prepend(LIBS=[lib])