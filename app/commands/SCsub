Import("env")
Import("env_app")

from glob import glob
import commands_builders

env.command_sources = []

env_cmd = env.Clone()

commands_gen_path = f'#app/commands/commands{env["GENSUFFIX"]}.cpp'
env_app["commands_gen_path"] = commands_gen_path

env_cmd.Depends(commands_gen_path, glob("*.inc"))
env_cmd.Depends(commands_gen_path, "#app/commands/commands_builders.py")
env_cmd.CommandNoCache(
    commands_gen_path,
    glob("*.inc"),
    env.Run(commands_builders.make_command_maps, "Building command maps.")
)

env_cmd.add_source_files(env.command_sources, "*.cpp")

env_cmd_gen = env_cmd.Clone()
if "-Werror" in env_cmd_gen["CCFLAGS"]:
    env_cmd_gen["CCFLAGS"].remove("-Werror")
env_cmd_gen.add_source_files(env.command_sources, commands_gen_path)

lib = env_cmd.add_library("commands", env.command_sources)
env.Prepend(LIBS=[lib])