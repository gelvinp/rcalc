Import("env")
from glob import glob

import commands_builders

env.command_sources = []

env_cmd = env.Clone()

env_cmd.Depends("#app/commands/commands.gen.cpp", glob("*.inc"))
env_cmd.Depends("#app/commands/commands.gen.cpp", "#app/commands/commands_builders.py")
env_cmd.CommandNoCache(
    "#app/commands/commands.gen.cpp",
    glob("*.inc"),
    env.Run(commands_builders.make_command_maps, "Building command maps.")
)

env_cmd.add_source_files(env.command_sources, "*.cpp")

env_cmd_gen = env_cmd.Clone()
if "-Werror" in env_cmd_gen["CCFLAGS"]:
    env_cmd_gen["CCFLAGS"].remove("-Werror")
env_cmd_gen.add_source_files(env.command_sources, "#app/commands/commands.gen.cpp")

lib = env_cmd.add_library("commands", env.command_sources)
env.Prepend(LIBS=[lib])